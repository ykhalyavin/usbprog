#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
# also update USBPROG_VERSION_STRING in usbprog/usbprog.h (for Win32)
# and win32/installer.nsi if you change the version number here
AC_INIT(usbprog, 0.1.9, bernhard.walle@gmx.de)
AC_CONFIG_SRCDIR([usbprog/usbprog.h])
AC_CONFIG_AUX_DIR(config)
AC_CONFIG_HEADERS([config.h])
AC_CANONICAL_SYSTEM


# Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_LIBTOOL
AC_CONFIG_MACRO_DIR([m4])
LT_INIT

# Checks for libraries.

AM_INIT_AUTOMAKE

#
# CURL
#
LIBCURL_CHECK_CONFIG([yes], [], [have_libcurl="yes"], [have_libcurl="no"])

if test "${have_libcurl}" != yes; then
	AC_MSG_ERROR([libcurl library not found, please install it])
else
	CPPFLAGS="$CPPFLAGS $LIBCURL_CPPFLAGS"
	LIBS="${LIBS} ${LIBCURL}"
fi

#
# strptime
#
AC_CHECK_FUNCS([strptime])

#
# readline
#
AC_CHECK_LIB([readline], [readline])

#
# libxml
#
AM_PATH_XML2([], [have_libxml="yes"], [have_libxml="no"])

if test "${have_libxml}" != yes; then
	AC_MSG_ERROR([libxml not found, please install it])
else
	CPPFLAGS="$CPPFLAGS $XML_CPPFLAGS"
	LIBS="$LIBS $XML_LIBS"
fi


#
# libusb
#
PKG_PROG_PKG_CONFIG()
PKG_CHECK_MODULES([usb], [libusb],[have_libusb="no"],[have_libusb="no"])

if test "${have_libusb}" != yes; then
    AC_CHECK_LIB([usb], [usb_init], [have_libusb="yes"], [have_libusb="no"])
    if test "${have_libusb}" == yes; then
        AC_CHECK_HEADER([usb.h], [have_libusb="yes"],[have_libusb="no"])
    fi

    if test "${have_libusb}" != yes; then
        AC_MSG_ERROR([libusb not found, please install it])
    else
        LIBS="$LIBS -lusb"
    fi
else
	CPPFLAGS="$CPPFLAGS $usb_CFLAGS"
	LIBS="$LIBS $usb_LIBS"
fi

#
# Big or little endian?
#
AC_C_BIGENDIAN
AC_SUBST(WORDS_BIGENDIAN)

#
# GUI?
#
AC_ARG_ENABLE([gui],
     [  --enable-gui		Enables the wxGtk UI (default)],
     [case "${enableval}" in
       yes) gui=true ;;
       no)  gui=false ;;
       *) AC_MSG_ERROR([bad value ${enableval} for --enable-gui]) ;;
     esac],[gui=true])

#
# wxGtk
#
if test x$gui = xtrue ; then
    AM_OPTIONS_WXCONFIG
    AM_PATH_WXCONFIG(2.6.0, wxWin=1)
    CPPFLAGS="$CPPFLAGS $WX_CPPFLAGS"
    CXXFLAGS="$CXXFLAGS $WX_CXXFLAGS_ONLY"
    CFLAGS="$CFLAGS $WX_CFLAGS_ONLY"
    AC_SUBST(WX_LIBS)
    #LIBS="$LIBS $WX_LIBS"
    if test x"${wx_ver_ok}" = xyes ; then
        gui=true
    else
        gui=false
    fi
fi

#
# GUI for Automake?
#
AM_CONDITIONAL([GUI], [test x$gui = xtrue])


# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST


AC_CONFIG_FILES([Makefile
                 md5/Makefile
                 usbprog/Makefile
                 gui/Makefile
                 src/Makefile])
AC_OUTPUT

#
# Print summary
#

echo
echo
echo
echo "---------------------------------------------------"
echo "           Build summary"
echo "---------------------------------------------------"
echo -n "Readline         : "
if test x"${ac_cv_lib_readline_readline}" = xyes ; then
    echo "enabled"
else
    echo "disabled"
fi

echo -n "GUI (wxWidgets)  : "
if test x"${gui}" = xtrue ; then
    echo "enabled"
else
    echo "disabled"
fi

echo "---------------------------------------------------"
echo


# vim: set sw=4 ts=4:
